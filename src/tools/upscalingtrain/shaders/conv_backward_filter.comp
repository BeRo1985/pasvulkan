#version 450
/*
 * conv_backward_filter.comp — Compute gradient w.r.t. weights (dW)
 *
 * Each thread computes one weight gradient element dW[oc][ic][kh][kw]
 * by iterating over all (batch, spatial) positions.
 *
 * Bindings: 0=input_act, 1=grad_output, 2=dW, 3=(unused/dummy)
 * Push constants: p0=in_ch, p1=out_ch, p2=ks, p3=pad, p4=H, p5=W, p6=batch
 *
 * Copyright (C) 2026 Benjamin 'BeRo' Rosseaux. License see PasVulkan.Framework.pas (zlib)
 */

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(push_constant) uniform PC {
    int p0, p1, p2, p3, p4, p5, p6, p7;
    float f0, f1, f2, f3, f4, f5, f6, f7;
} pc;

layout(set = 0, binding = 0) readonly  buffer BufI  { float inp[];      };
layout(set = 0, binding = 1) readonly  buffer BufGO  { float grad_out[]; };
layout(set = 0, binding = 2)           buffer BufDW  { float dW[];       };
layout(set = 0, binding = 3) readonly  buffer BufDum { float dummy[];    };

void main() {
    int idx = int(gl_GlobalInvocationID.x);

    int in_ch  = pc.p0;
    int out_ch = pc.p1;
    int ks     = pc.p2;
    int pad    = pc.p3;
    int H      = pc.p4;
    int W      = pc.p5;
    int batch  = pc.p6;

    int total_weights = out_ch * in_ch * ks * ks;
    if (idx >= total_weights) return;

    /* Decode linear index → (oc, ic, kh, kw) */
    int kw = idx % ks;
    int kh = (idx / ks) % ks;
    int ic = (idx / (ks * ks)) % in_ch;
    int oc = idx / (ks * ks * in_ch);

    float sum = 0.0;
    for (int n = 0; n < batch; n++) {
        int g_base = (n * out_ch + oc) * H * W;
        int i_base = (n * in_ch  + ic) * H * W;
        for (int oh = 0; oh < H; oh++) {
            int ih = oh - pad + kh;
            if (ih < 0 || ih >= H) continue;
            for (int ow = 0; ow < W; ow++) {
                int iw = ow - pad + kw;
                if (iw >= 0 && iw < W) {
                    sum += grad_out[g_base + oh * W + ow]
                         * inp[i_base + ih * W + iw];
                }
            }
        }
    }

    dW[idx] += sum;
}
