#version 450 core

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable

/* clang-format off */

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(push_constant, std140, row_major) uniform PushConstants {
  uvec4 tileSizeZNearZFar; 
  vec4 viewRect;
  uvec4 countLightsViewIndexSizeOffsetedViewIndex;  
  uvec4 clusterSize;
  vec4 scaleBiasMax;
} pushConstants;

struct View {
  mat4 viewMatrix;
  mat4 projectionMatrix;
  mat4 inverseViewMatrix;
  mat4 inverseProjectionMatrix;
};

layout(std140, set = 0, binding = 0) uniform uboViews {
  View views[256]; // 65536 / (64 * 4) = 256
} uView;

struct AABB {
  vec4 minViewSpaceBounds;
  vec4 maxViewSpaceBounds; 
  vec4 minWorldSpaceBounds;
  vec4 maxWorldSpaceBounds;
};

layout(std430, set = 0, binding = 1) buffer LightGridClusterAABBs {
  AABB lightGridClusterAABBs[];
};

layout (std430, set = 0, binding = 2) buffer LightGridIndexListCounter {
  uint lightGridIndexListCounter;
};
/* clang-format on */

mat4 invMat;

vec4 screen2Eye(vec4 coord) {
  vec2 ndc = fma((coord.xy - pushConstants.viewRect.xy) / pushConstants.viewRect.zw, vec2(2.0), vec2(-1.0));
  vec4 eye = invMat * vec4(ndc, coord.z, 1.0);
  return (eye / eye.w);
}

vec3 lineIntersectionToZPlane(vec3 firstPoint, vec3 secondPoint, float zValueViewSpace){
  vec3 lineDirection = secondPoint - firstPoint;
  return fma(lineDirection, vec3((zValueViewSpace - firstPoint.z) / lineDirection.z), firstPoint);
}

void main() {
  //

  View view = uView.views[pushConstants.countLightsViewIndexSizeOffsetedViewIndex.w];

  invMat = view.inverseProjectionMatrix;

  const float infinity = uintBitsToFloat(0x7f800000u);

  uvec3 globalInvocationID = uvec3(gl_GlobalInvocationID.xyz); //(gl_WorkGroupID.xyz * gl_WorkGroupSize.xyz) + gl_LocalInvocationID.xyz; 
  uvec3 globalSize = uvec3(gl_NumWorkGroups.xyz * gl_WorkGroupSize.xyz); 
  uint globalInvocationIndex = (((globalInvocationID.z * globalSize.y) + globalInvocationID.y) * globalSize.x) + globalInvocationID.x;
 
  uint clusterIndex = globalInvocationIndex + (pushConstants.countLightsViewIndexSizeOffsetedViewIndex.y * pushConstants.countLightsViewIndexSizeOffsetedViewIndex.z);

#ifdef REVERSEDZ
  vec4 minScreen = vec4(vec2(uvec2(globalInvocationID.xy) * pushConstants.tileSizeZNearZFar.xy), 1.0, 1.0);
  vec4 maxScreen = vec4(vec2((uvec2(globalInvocationID.xy) + uvec2(1)) * pushConstants.tileSizeZNearZFar.xy), 1.0, 1.0);
#else
  vec4 minScreen = vec4(vec2(uvec2(globalInvocationID.xy) * pushConstants.tileSizeZNearZFar.xy), 0.0, 1.0);
  vec4 maxScreen = vec4(vec2((uvec2(globalInvocationID.xy) + uvec2(1)) * pushConstants.tileSizeZNearZFar.xy), 0.0, 1.0);
#endif

  vec3 minEye = screen2Eye(minScreen).xyz;
  vec3 maxEye = screen2Eye(maxScreen).xyz;

  vec2 ZNearZFar = uintBitsToFloat(pushConstants.tileSizeZNearZFar.zw);

  float clusterNear = (globalInvocationID.z == 0u) ? 0.0 : (ZNearZFar.x * pow(ZNearZFar.y / ZNearZFar.x, globalInvocationID.z / float(globalSize.z)));
  float clusterFar = ((globalInvocationID.z + 1u) == globalSize.z)  //
                         ? 3.4e+32  // infinity                     // the last cluster depth row is going into the infinity for a reserved z projection matrix with an actually infinite z far plane
                         : (ZNearZFar.x * pow(ZNearZFar.y / ZNearZFar.x, (globalInvocationID.z + 1) / float(globalSize.z)));

#if 0
  vec3 minNear = (minEye * clusterNear) / minEye.z;
  vec3 minFar = (minEye * clusterFar) / minEye.z;
  vec3 maxNear = (maxEye * clusterNear) / maxEye.z;
  vec3 maxFar = (maxEye * clusterFar) / maxEye.z;
#else
  vec3 minNear = lineIntersectionToZPlane(vec3(0.0), minEye, -clusterNear);
  vec3 minFar = lineIntersectionToZPlane(vec3(0.0), minEye, -clusterFar); 
  vec3 maxNear = lineIntersectionToZPlane(vec3(0.0), maxEye, -clusterNear);
  vec3 maxFar = lineIntersectionToZPlane(vec3(0.0), maxEye, -clusterFar); 
#endif

  vec3 aabbViewSpaceMin = min(min(minNear, minFar), min(maxNear, maxFar));
  vec3 aabbViewSpaceMax = max(max(minNear, minFar), max(maxNear, maxFar));
  
  mat4 viewSpaceToWorldSpaceMatrix = view.inverseViewMatrix;

  vec4 aabbCorners[8] = {
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMin.x, aabbViewSpaceMin.y, aabbViewSpaceMin.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMin.x, aabbViewSpaceMin.y, aabbViewSpaceMax.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMin.x, aabbViewSpaceMax.y, aabbViewSpaceMin.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMin.x, aabbViewSpaceMax.y, aabbViewSpaceMax.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMax.x, aabbViewSpaceMin.y, aabbViewSpaceMin.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMax.x, aabbViewSpaceMin.y, aabbViewSpaceMax.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMax.x, aabbViewSpaceMax.y, aabbViewSpaceMin.z, 1.0),  //
    viewSpaceToWorldSpaceMatrix * vec4(aabbViewSpaceMax.x, aabbViewSpaceMax.y, aabbViewSpaceMax.z, 1.0),  //
  };
  aabbCorners[0] /= aabbCorners[0].w;
  aabbCorners[1] /= aabbCorners[1].w;
  aabbCorners[2] /= aabbCorners[2].w;
  aabbCorners[3] /= aabbCorners[3].w;
  aabbCorners[4] /= aabbCorners[4].w;
  aabbCorners[5] /= aabbCorners[5].w;
  aabbCorners[6] /= aabbCorners[6].w;
  aabbCorners[7] /= aabbCorners[7].w;
  vec3 aabbWorldSpaceMin = min(min(min(min(min(min(min(aabbCorners[0], aabbCorners[1]), aabbCorners[2]), aabbCorners[3]), aabbCorners[4]), aabbCorners[5]), aabbCorners[6]), aabbCorners[7]).xyz;
  vec3 aabbWorldSpaceMax = max(max(max(max(max(max(max(aabbCorners[0], aabbCorners[1]), aabbCorners[2]), aabbCorners[3]), aabbCorners[4]), aabbCorners[5]), aabbCorners[6]), aabbCorners[7]).xyz;

  vec3 center = (aabbViewSpaceMin + aabbViewSpaceMax) * 0.5;
  vec3 extents = (aabbViewSpaceMax - aabbViewSpaceMin) * 0.5;
  float radius = length(extents);

  lightGridClusterAABBs[clusterIndex].minViewSpaceBounds = vec4(aabbViewSpaceMin, center.x);
  lightGridClusterAABBs[clusterIndex].maxViewSpaceBounds = vec4(aabbViewSpaceMax, center.y);
  lightGridClusterAABBs[clusterIndex].minWorldSpaceBounds = vec4(aabbWorldSpaceMin, center.z);
  lightGridClusterAABBs[clusterIndex].maxWorldSpaceBounds = vec4(aabbWorldSpaceMax, radius);

  if(clusterIndex == 0u){
    lightGridIndexListCounter = 0u; 
  }

}
