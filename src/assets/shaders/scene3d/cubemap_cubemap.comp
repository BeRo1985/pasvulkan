#version 450 core

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable

/* clang-format off */

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

/* clang-format on */

#ifdef USE_RGB9E5
layout(set = 0, binding = 0, r32ui) writeonly uniform uimageCube uDestinationTexture;
#else
layout(set = 0, binding = 0) writeonly uniform imageCube uDestinationTexture;
#endif
layout(set = 0, binding = 1) uniform samplerCube uSourceTexture;

ivec2 destinationTextureSize = imageSize(uDestinationTexture);         

#include "cubemap.glsl"

#ifdef USE_RGB9E5
#include "rgb9e5.glsl"
#endif

void main() {
  ivec3 uvw = ivec3(gl_GlobalInvocationID.xyz);
  if (all(greaterThanEqual(uvw, ivec3(0))) && all(lessThan(uvw, ivec3(destinationTextureSize, 6)))) {
    vec3 direction = getCubeMapDirection((vec2(uvw.xy) + vec2(0.5)) / vec2(destinationTextureSize), uvw.z);
    vec4 c = textureLod(uSourceTexture, direction, 0.0) * vec2(0.5, 1.0).xxxy; // Scale the color to fit into the range of 16-bit floating point numbers, by converting to half lux, so that the sun can be represented as 64000 "bi-lux" instead of 120000 lux.
#ifdef USE_RGB9E5
    imageStore(uDestinationTexture, uvw, uvec4(encodeRGB9E5(c.xyz)));
#else
    imageStore(uDestinationTexture, uvw, clamp(c, vec4(-65504.0), vec4(65504.0))); // Clamp to the range of 16-bit floating point numbers.
#endif
  }
}