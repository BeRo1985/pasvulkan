#version 450 core

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable

/* clang-format off */

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

/* clang-format on */

#ifdef USE_RGB9E5
#undef ALPHA
layout(set = 0, binding = 0, r32ui) writeonly uniform uimageCube uDestinationTexture;
#elif defined(USE_RGBA32F)
#define ALPHA
layout(set = 0, binding = 0, rgba32f) writeonly uniform imageCube uDestinationTexture;
#elif defined(USE_RGBA16F)
#define ALPHA
layout(set = 0, binding = 0, rgba16f) writeonly uniform imageCube uDestinationTexture;
#elif defined(USE_R11G11B10F)
#undef ALPHA
layout(set = 0, binding = 0, r11f_g11f_b10f) writeonly uniform imageCube uDestinationTexture;
#elif defined(USE_RGBA8)
#define ALPHA
layout(set = 0, binding = 0, rgba8) writeonly uniform imageCube uDestinationTexture;
#elif defined(USE_R8_SNORM)
layout(set = 0, binding = 0, r8_snorm) writeonly uniform imageCube uDestinationTexture;
#elif defined(USE_R8)
layout(set = 0, binding = 0, r8) writeonly uniform imageCube uDestinationTexture;
#else
layout(set = 0, binding = 0) writeonly uniform imageCube uDestinationTexture;
#endif
layout(set = 0, binding = 1) uniform sampler2D uSourceTexture;

ivec2 destinationTextureSize = imageSize(uDestinationTexture);         

#include "cubemap.glsl"

#ifdef USE_RGB9E5
#include "rgb9e5.glsl"
#endif

#include "textureutils.glsl"

#include "octahedral.glsl"

#include "octahedralmap.glsl"

void main() {
  ivec3 uvw = ivec3(gl_GlobalInvocationID.xyz);
  if (all(greaterThanEqual(uvw, ivec3(0))) && all(lessThan(uvw, ivec3(destinationTextureSize, 6)))) {
    vec3 direction = getCubeMapDirection((vec2(uvw.xy) + vec2(0.5)) / vec2(destinationTextureSize), uvw.z);
#ifdef PLANET_OCTAHEDRAL
    vec4 c = texturePlanetOctahedralMap(uSourceTexture, direction, 0);
#else    
    vec4 c = textureOctahedralMap(uSourceTexture, direction, 0);
#endif
#ifdef USE_RGB9E5
    imageStore(uDestinationTexture, uvw, uvec4(encodeRGB9E5(c.xyz)));
#elif defined(USE_RGBA16F)
    imageStore(uDestinationTexture, uvw, clamp(c, vec4(-65504.0), vec4(65504.0))); // Clamp to the range of 16-bit floating point numbers.
#elif defined(USE_R11G11B10F)
    imageStore(uDestinationTexture, uvw, clamp(c, vec4(0.0), vec4(65000.0))); // Clamp to the approximately representable range of 11-bit/10-bit floating point numbers.
#else   
    imageStore(uDestinationTexture, uvw, c); // Store the raw value, no conversion, no clamping, no encoding, just store the value as is.
#endif
  }
}