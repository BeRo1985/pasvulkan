#version 450 core

#pragma shader_stage(compute)

// Water simulation based on "Fast Hydraulic Erosion Simulation and Visualization on GPU" by Xing Mei, Philippe Decaudin, Bao-Gang Hu
// Link: https://hal.inria.fr/inria-00402079/document

// But yet without the erosion part of the terrain part, just the water flow simulation for now.

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_samplerless_texture_functions : enable
#extension GL_GOOGLE_include_directive : enable

#if 0
// 16-bit floats
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int16 : enable
#extension GL_EXT_shader_16bit_storage : enable
#endif

/* clang-format off */

#define WORKGROUP_SIZE 16

layout(local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

layout(push_constant) uniform PushConstants {
  float attenuation;
  float strength;
  float minTotalFlow;
  float initialWaterLevel;      
  float pipeLengthSquared;
  float crossSectionalPipeArea;
  float gravity;
  float evaporation;    
  float compensationFactor;
  float bottomRadius;
  float topRadius;
  float deltaTime;
  uint planetHeightMapResolution;  
  uint waterHeightMapResolution; 
  uint waterHeightMapBorder;
  uint frameIndex;
} pushConstants;

uint waterHeightMapResolutionWithBorder = pushConstants.waterHeightMapResolution + (pushConstants.waterHeightMapBorder * 2);

layout(set = 0, binding = 0) uniform sampler2D uPlanetHeightmap;

#if 0
#define _float float16_t
#define _vec4 f16vec4
#else
#define _float float
#define _vec4 vec4
#endif

layout(set = 0, binding = 1, std430) readonly buffer InWaterHeightMap {
  _float values[];
} inWaterHeightMap;

layout(set = 0, binding = 2, std430) writeonly buffer OutWaterHeightMap {
  _float values[];
} outWaterHeightMap;

layout(set = 0, binding = 3, std430) readonly buffer WaterFlowMap {
  _vec4 values[];
} waterFlowMap;

layout(set = 0, binding = 4, std430) coherent buffer WaterMaxHeightDifference {
  uint value;
} waterMaxHeightDifference;

#include "octahedral.glsl"
#include "octahedralmap.glsl"

vec2 texelSize = 1.0 / vec2(pushConstants.waterHeightMapResolution);

uint getIndex(ivec2 position){
  const ivec2 intPosition = wrapOctahedralTexelCoordinatesForWater(ivec2(position), ivec2(pushConstants.waterHeightMapResolution));  
  return (intPosition.y * pushConstants.waterHeightMapResolution) + intPosition.x;
}

uint getFlowMapIndex(ivec2 position){
  position = (((position + ivec2(int(pushConstants.waterHeightMapBorder))) % ivec2(int(waterHeightMapResolutionWithBorder))) + ivec2(int(waterHeightMapResolutionWithBorder))) % ivec2(int(waterHeightMapResolutionWithBorder));
  return (position.y * waterHeightMapResolutionWithBorder) + position.x;
}

shared vec4 sharedFlowMapValues[WORKGROUP_SIZE + 2][WORKGROUP_SIZE + 2]; // WORKGROUP_SIZE x WORKGROUP_SIZE + 1x1 for the border on each side for the 3x3 kernel 

shared uint sharedWrappedFlowMapCoordinateIndices[WORKGROUP_SIZE + 2][WORKGROUP_SIZE + 2];

shared float sharedHeightDifferences[WORKGROUP_SIZE * WORKGROUP_SIZE];

void main(){

  if(pushConstants.frameIndex != 0){

    const ivec2 localPos = ivec2(gl_LocalInvocationID.xy);
    const ivec2 tileOrigin = (ivec2(gl_WorkGroupID.xy) * WORKGROUP_SIZE) - ivec2(1, 1);
      
    const int dimension = WORKGROUP_SIZE + 2;
    const int totalElements = dimension * dimension;
    const int threadID = (localPos.y * WORKGROUP_SIZE) + localPos.x;
          
    for(int i = threadID; i < totalElements; i += 256){
      ivec2 t = ivec2(i % dimension, i / dimension);
      sharedFlowMapValues[t.y][t.x] = waterFlowMap.values[sharedWrappedFlowMapCoordinateIndices[t.y][t.x] = getFlowMapIndex(tileOrigin + t)];
    }

  }

  memoryBarrierShared();
  barrier();

  const uvec2 position = uvec2(gl_GlobalInvocationID.xy);

  if(all(lessThan(position, uvec2(pushConstants.waterHeightMapResolution)))){

    const uint heightMapIndex = (position.y * pushConstants.waterHeightMapResolution) + position.x;

    const uint flowMapIndex = ((position.y + uint(pushConstants.waterHeightMapBorder)) * waterHeightMapResolutionWithBorder) + (position.x + uint(pushConstants.waterHeightMapBorder));

    float waterOutHeight;

    if(pushConstants.frameIndex == 0){

      // The first frame is a special case and the easy one, we just need to initialize the water height map.     
      waterOutHeight = pushConstants.initialWaterLevel;

      // Initialize the shared height difference to zero for the first frame.
      sharedHeightDifferences[gl_LocalInvocationIndex] = 0.0;

    }else{

      // Calculate the total inflow
      const float totalInFlow = dot(vec4(waterFlowMap.values[flowMapIndex]), vec4(1.0));

      uvec4 flowMapNeighbours = uvec4(
        sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 2], // ivec2( 1,  0)
        sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y + 2][gl_LocalInvocationID.x + 1], // ivec2( 0,  1)
        sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 0], // ivec2(-1,  0)
        sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y + 0][gl_LocalInvocationID.x + 1]  // ivec2( 0, -1)
      );
                   
      // Calculate the total outflow
      float totalOutFlow;

      // Check that the wrapped coordinates are unique
      if(any(equal(flowMapNeighbours.xxx, flowMapNeighbours.yzw)) || any(equal(flowMapNeighbours.yyy, flowMapNeighbours.xzw)) || any(equal(flowMapNeighbours.zzz, flowMapNeighbours.xyw)) || any(equal(uvec4(sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y][gl_LocalInvocationID.x]), flowMapNeighbours))){

        // If the wrapped coordinates are not unique, we need to check for duplicates and use each one only once
        // This is a bit tricky, but we can use the fact that the sharedFlowMapValues array is 2D and we can check for duplicates
        // Check for duplicates and use each one only once

        // Get the flow values for the 3x3 kernel in advance
        vec4 flowValues = vec4(
          sharedFlowMapValues[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 2].z, // ivec2( 1,  0)
          sharedFlowMapValues[gl_LocalInvocationID.y + 2][gl_LocalInvocationID.x + 1].w, // ivec2( 0,  1)
          sharedFlowMapValues[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 0].x, // ivec2(-1,  0)
          sharedFlowMapValues[gl_LocalInvocationID.y + 0][gl_LocalInvocationID.x + 1].y  // ivec2( 0, -1)
        );

        // Initialize the total outflow to zero
        totalOutFlow = 0.0;

        // Check for duplicates and use each one only once
        // We can use a simple loop to check for duplicates and use each one only once
        uvec4 uniqueCoordinateIndices = uvec4(sharedWrappedFlowMapCoordinateIndices[gl_LocalInvocationID.y][gl_LocalInvocationID.x]); 
        for(int i = 0, count = 0; i < 4; i++){
          if(!any(equal(uvec4(flowMapNeighbours[i]), uniqueCoordinateIndices))){
            uniqueCoordinateIndices[count] = flowMapNeighbours[i];
            totalOutFlow += flowValues[i];
            count++;
          }
        }
       
      }else{

        // If all wrapped coordinates are unique, we can use them directly without further non-unique checks for faster performance
        // Just sum the flow values for the 3x3 kernel   
         
        totalOutFlow = sharedFlowMapValues[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 2].z + // ivec2( 1,  0)
                       sharedFlowMapValues[gl_LocalInvocationID.y + 2][gl_LocalInvocationID.x + 1].w + // ivec2( 0,  1)
                       sharedFlowMapValues[gl_LocalInvocationID.y + 1][gl_LocalInvocationID.x + 0].x + // ivec2(-1,  0)
                       sharedFlowMapValues[gl_LocalInvocationID.y + 0][gl_LocalInvocationID.x + 1].y;  // ivec2( 0, -1)

      }

      // Read the water height from the input water height map
      const float inWaterHeight = inWaterHeightMap.values[heightMapIndex];

      // Calculate the new water height
      waterOutHeight = max(0.0, (inWaterHeight + (((totalOutFlow - totalInFlow) * pushConstants.deltaTime) / pushConstants.pipeLengthSquared)) * (1.0 - clamp(pushConstants.evaporation * pushConstants.deltaTime, 0.0, 1.0)));

      // Calculate the absolute height differences
      sharedHeightDifferences[gl_LocalInvocationIndex] = abs(inWaterHeight - waterOutHeight);

    }

    outWaterHeightMap.values[heightMapIndex] = _float(waterOutHeight);

    // Perform a parallel max reduction to calculate the total absolute height differences

    memoryBarrierShared();
    barrier();

#if WORKGROUP_SIZE != 16   

    // Automatic unrolled version of the parallel max reduction for WORKGROUP_SIZE != 16
    
    [[unroll]] for(uint i = 128u; i > 0u; i >>= 1u){
      if(gl_LocalInvocationIndex < i){
        sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + i]);
      }
      memoryBarrierShared();
      barrier();
    }

#else

    // Manual unrolled version of the parallel max reduction for WORKGROUP_SIZE == 16

    if(gl_LocalInvocationIndex < 128){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 128]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 64){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 64]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 32){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 32]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 16){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 16]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 8){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 8]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 4){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 4]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 2){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 2]);
    }
    memoryBarrierShared();
    barrier();

    if(gl_LocalInvocationIndex < 1){
      sharedHeightDifferences[gl_LocalInvocationIndex] = max(sharedHeightDifferences[gl_LocalInvocationIndex], sharedHeightDifferences[gl_LocalInvocationIndex + 1]);
    }
    memoryBarrierShared();
    barrier();

#endif

    if(gl_LocalInvocationIndex == 0){
      atomicMax(waterMaxHeightDifference.value, floatBitsToUint(sharedHeightDifferences[0]));
    }    

  }

}