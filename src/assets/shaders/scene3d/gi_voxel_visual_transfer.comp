#version 450 core

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_control_flow_attributes : enable
//#extension GL_EXT_scalar_block_layout : enable

/* clang-format off */

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (set = 0, binding = 0, std140) readonly uniform VoxelGridData {
  vec4 clipMaps[4]; // xyz = center in world-space, w = extent of a voxel 
  uint gridSize; // number of voxels in a clipmap in a single dimension
  uint countClipMaps; // maximum 4 clipmaps
  uint hardwareConservativeRasterization; // 0 = false, 1 = true
} voxelGridData;

layout (set = 0, binding = 1, std430) readonly buffer VoxelGridColors {
#if defined(USESHADERBUFFERFLOAT32ATOMICADD)
  vec4 data[]; // 32bit floating point
#else
  uvec4 data[]; // 22.12bit fixed point
#endif
} voxelGridColors;

layout (set = 0, binding = 2, std430) readonly buffer VoxelGridCounters {
  uint data[]; // 32-bit fixed point
} voxelGridCounters;

layout(set = 0, binding = 3, rgba16f) writeonly uniform image3D uImageVolume[]; 

void main(){

  uint voxelGridSize = voxelGridData.gridSize;

  uvec3 invocation = uvec3(gl_GlobalInvocationID.xyz);

  if(all(greaterThanEqual(invocation, uvec3(0))) && all(lessThan(invocation, uvec3(uvec2(voxelGridSize), voxelGridSize * voxelGridData.countClipMaps)))){

    uint clipMapIndex = invocation.z / voxelGridSize;

    uvec3 volumePosition = uvec3(invocation.xy, invocation.z - (clipMapIndex * voxelGridSize));

    uint volumeBaseIndex = ((((((uint(clipMapIndex) * voxelGridSize) + uint(volumePosition.z)) * voxelGridSize) + uint(volumePosition.y)) * voxelGridSize) + uint(volumePosition.x)) * 6;

    [[unroll]]
    for(uint anisotropicAxisDirectionIndex = 0; anisotropicAxisDirectionIndex < 6; anisotropicAxisDirectionIndex++){

      uint volumeIndex = volumeBaseIndex + anisotropicAxisDirectionIndex;

      imageStore(
        uImageVolume[(clipMapIndex * 6) + anisotropicAxisDirectionIndex], 
        ivec3(volumePosition), 
#if defined(USESHADERBUFFERFLOAT32ATOMICADD)
        vec4(vec4(voxelGridColors.data[volumeIndex]) / max(1u, voxelGridCounters.data[volumeIndex]))
#else
        vec4(vec4(voxelGridColors.data[volumeIndex]) / (max(1u, voxelGridCounters.data[volumeIndex]) * 4096.0))
#endif
      );
      
    } 

  }

}

