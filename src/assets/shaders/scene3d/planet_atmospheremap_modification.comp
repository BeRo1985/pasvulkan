#version 450 core

#pragma shader_stage(compute)

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable

/* clang-format off */

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0, r8) uniform image2D uImageAtmosphereMap;

layout(set = 0, binding = 1) uniform sampler2DArray uBrushTextureArray[];

layout(push_constant) uniform PushConstants {
  vec4 positionRadius; // xyz = position, w = radius
  vec4 innerRadiusValueMinMax; // x = inner radius, y = value, z = min, w = max
  uint brushIndex;
  float brushRotation;
} pushConstants;

/* clang-format on */

#include "octahedral.glsl"
#include "octahedralmap.glsl"
#include "planet_brushes.glsl"

void main(){
  
  ivec2 texel = ivec2(gl_GlobalInvocationID.xy);

  ivec2 texSize = imageSize(uImageAtmosphereMap);

  if(all(greaterThanEqual(texel, ivec2(0))) && all(lessThan(texel, texSize))){

    vec3 p = octPlanetUnsignedDecode(vec2(texel) / vec2(texSize));

    vec3 n = normalize(pushConstants.positionRadius.xyz);

    float d = sampleBrush(
      p,
      n,
      pushConstants.positionRadius.w,
      pushConstants.innerRadiusValueMinMax.x,
      pushConstants.brushIndex,
      pushConstants.brushRotation
    );

    if(d > 0.0){

      float value = imageLoad(uImageAtmosphereMap, texel).x + (d * pushConstants.innerRadiusValueMinMax.y);
          
      imageStore(
        uImageAtmosphereMap, 
        texel, 
        vec4(
          clamp(
            value, 
            pushConstants.innerRadiusValueMinMax.z, 
            pushConstants.innerRadiusValueMinMax.w
          )
        )
      );

    }

  }

}

